# This SLAMD script tests the 0 page Authentication rate of Identity Server
#
# Valid Parameters:
# loginUrl=URL of the Identity Server Login Services
# uid=UID, where UID could be static(user1), sequential(user[1:100]), or random (user[1-100])
# password=password where password is a static value (or you could modify the script)



# Make the HTTP data types available for use.
use com.sun.slamd.scripting.http.HTTPClientVariable;
use com.sun.slamd.scripting.http.HTTPRequestVariable;
use com.sun.slamd.scripting.http.HTTPResponseVariable;



# Define the variables we will use.
variable string         loginUrl;
variable ValuePattern   uid;
variable string         password;
variable string         authErrorCode;
variable HTTPClient     httpClient;
variable HTTPRequest    request;
variable HTTPResponse    response;
variable IncrementalTracker authAttempts;
variable IncrementalTracker authSuccesses;
variable IncrementalTracker authFailures;
variable TimeTracker        authTimer;

# Initialize the stat trackers.
authAttempts.setDisplayName("Attempted Authentications");
authSuccesses.setDisplayName("Successful Authentications");
authFailures.setDisplayName("Failed Authentications");
authTimer.setDisplayName("Authentication Time (ms)");

# Specify the host, port, uid, and password.
loginUrl.assign(script.getScriptArgument("loginUrl", "http://ips-perf1.red.iplanet.com:58080/amserver/UI/Login"));
uid.assign(script.getScriptArgument("uid", "user[1-100]"));
password.assign(script.getScriptArgument("password", "password"));
httpClient.setBlindTrust(true);
httpClient.setFollowRedirects(false);
    
# Start the stat trackers.
authAttempts.startTracker();
authSuccesses.startTracker();
authFailures.startTracker();
authTimer.startTracker();



# Loop forever.  This will be automatically stopped when the stop time has
# arrived or the job has been run for the maximum allowed duration.
while script.shouldNotStop()
begin

    authAttempts.increment();    
    request.assign("POST",loginUrl);
    request.addParameter("Login.Token1",uid.nextValue());
    request.addParameter("Login.Token2",password);
    request.setHeader("Content-Type", "application/x-www-form-urlencoded");          
    authTimer.startTimer();
    response = httpClient.sendRequest( request );
    authTimer.stopTimer();
    httpClient.clearCookies();

    # See if the connection was successful.
    authErrorCode.assign(response.getHeader("X-autherrorcode"));
    if authErrorCode.equals("0",false)
        authSuccesses.increment();
    else
        authFailures.increment();
    
end;

# Stop the stat trackers.
authAttempts.stopTracker();
authSuccesses.stopTracker();
authFailures.stopTracker();
authTimer.stopTracker();



